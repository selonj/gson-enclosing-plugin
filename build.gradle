group 'com.selonj'
version '1.1'

apply plugin: 'java'
apply plugin: 'maven'

def jdkVersion = 1.7
def sourceFileEncoding = 'UTF-8'
sourceCompatibility = jdkVersion
targetCompatibility = jdkVersion

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
}

dependencies {
    compile 'com.google.code.gson:gson:2.2.4'
    testCompile('junit:junit:4.12') {
        exclude group: 'org.hamcrest'
    }
    testCompile 'org.hamcrest:hamcrest-library:1.3'
}

tasks.withType(JavaCompile) {
    options.encoding = sourceFileEncoding
}

sourceSets.all {
    resources.filter.exclude '**/.keep'
}

apply plugin: 'idea'
idea {
    module {
        excludeDirs = [file('.idea'), file('.gradle'), file('gradle')]
        downloadSources = false
    }
}

task exportAsMavenProject {
    pom {
        whenConfigured { pom ->
            pom.project {
                properties = ['project.build.sourceFileEncoding': sourceFileEncoding, 'compilerVersion': jdkVersion.toString()]
            }
        }
        withXml {
            def build = NodeBuilder.newInstance().build {
                plugins {
                    plugin {
                        groupId('org.apache.maven.plugins')
                        artifactId('maven-compiler-plugin')
                        version('3.3')
                        configuration {
                            source('${compilerVersion}')
                            target('${compilerVersion}')
                        }
                    }
                }
            };
            asNode().append(build);
        }
    }.writeTo(file('pom.xml'))
}
